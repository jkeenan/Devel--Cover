#!/usr/bin/perl

# Copyright 2001-2013, Paul Johnson (paul@pjcj.net)

# This software is free.  It is licensed under the same terms as Perl itself.

# The latest version of this software should be available from my homepage:
# http://www.pjcj.net

require 5.6.1;

use strict;
use warnings;

# VERSION

use Devel::Cover::DB;
use Devel::Cover::cover::Defaults qw(
    %defaults
    get_options
);
use Devel::Cover::cover;

# main program

if (!$ENV{DEVEL_COVER_SELF} && $INC{"Devel/Cover.pm"})
{
    my $err = "$0 shouldn't be run with coverage turned on.\n";
    eval
    {
        require POSIX;
        print STDERR $err;
        POSIX::_exit(1);
    };
    die $err;
}

my $config = get_options(\%defaults);

my $self = Devel::Cover::cover->new($config);
exit 0 unless defined $self;

if ($self->{delete}) {
    $self->delete_db(@ARGV);
    exit 0;
}

my $test_result = 0;
if ($self->{test}) {
    $test_result = $self->run_test(@ARGV);
}

if ($self->{gcov}) {
    $self->run_gcov();
}

my $rv = $self->prepare_db();
exit 0 unless defined $rv;

$self->merge_databases(@ARGV);

if ($self->{dump_db}) {
    $self->dump_db();
    exit 0;
}

if (exists $self->{write}) {
    $self->write_db();
}

# Can $self->{report} ever be false?
exit 0 unless $self->{summary} || $self->{report};

$self->prepare_summary();

print "\n\n" unless $self->{silent};

$self->second_prepare_db();

if ($self->{summary}) {
    $self->execute_summary();
}

# Can length($self->{report}) ever be 0?
exit $test_result unless length $self->{report};

$self->execute_report();

if ($self->{launch}) {
    $self->launch();
}

exit $test_result;

__END__

=head1 NAME

refactor_cover - report coverage statistics

=head1 SYNOPSIS

 cover -help -info -version
       -summary -report report_format -outputdir dir
       -select filename -ignore filename
       -select_re RE -ignore_re RE
       -write [db] -delete -dump_db -launch
       -silent
       -coverage criterion
       -test -gcov
       -make [make]
       -add_uncoverable_point -delete_uncoverable_point
       -clean_uncoverable_points -uncoverable_file
       [report specific options]
       coverage_database [coverage_database ...]

=head1 DESCRIPTION

Report coverage statistics in a variety of formats.

The summary option produces a short textual summary.  Other reports are
available by using the report option.

The following reports are currently available:

 text                  - detailed textual summary
 html                  - detailed HTML reports
 html_basic            - detailed HTML reports with syntax highlighting
 compilation           - output in a format similar to Perl

=head1 OPTIONS

The following command line options are supported:

 -h -help              - show help
 -i -info              - show documentation
 -v -version           - show version

 -silent               - don't print informational messages (default off)
 -summary              - give summary report                (default on)
 -report report_format - report format                      (default html)
 -outputdir            - directory for output               (default db)
 -launch               - launch report in viewer (if avail) (default off)

 -select filename      - only report on the file            (default all)
 -ignore filename      - don't report on the file           (default none)
 -select_re RE         - append to REs of files to select   (default none)
 -ignore_re RE         - append to REs of files to ignore   (default none)
 -write [db]           - write the merged database          (default off)
 -delete               - drop database(s)                   (default off)
 -dump_db              - dump database(s) (for debugging)   (default off)

 -coverage criterion   - report on criterion  (default all available)

 -test                 - drop database(s) and run make test (default off)
 -gcov                 - run gcov to cover XS code     (default on if using gcc)
 -make make_prog       - use the given 'make' program for 'make test'

 other options specific to the report format

=head1 REPORT FORMATS

Tool accepts -report option:

=over 4

=item html|html_minimal (default)

HTML reporting. Percentage thresholds are color-coded and configurable
via -report_c0 <integer>, -report_c1 <integer> and -report_c2 <integer>.:

    0%      75%      90%      100%
    |   ..   |   ..   |   ..   |
       <c0      <c1      <c2   c3
       red     yellow   orange green

=item html_basic

HTML reporting with syntax highlighting if L<PPI::HTML> or L<Perl::Tidy>
module is detected. Like html|html_minimal reporting, percentage thresholds
are color-coded and configurable.

=item text

Plain text reporting.

=item compilation

Like text but hacked to give a minimal output in a format similar
to that output by Perl itself so that it's easier to step through
the untested locations with Emacs compilation mode.

=back

=head1 DETAILS

Any number of coverage databases may be specified on the command line.
These databases will be merged and the reports will be based on the
merged information.  If no databases are specified the default database
(cover_db) will be used.

The -write option will write out the merged database.  If no name is
given for the new database, the first database read in will be
overwritten.  When this option is used no reports are generated by
default.

Specify -select and -ignore options to report on specific files.

Specify -coverage options to report on specific criteria.  By default all
available information on all criteria in all files will be reported.
Available coverage options are statement, branch, condition, subroutine, and
pod.  However, if you know you only want coverage information for certain
criteria it is better to only collect data for those criteria in the first
place by specifying them at that point.  This will make the data collection
and reporting processes faster and less memory intensive.  See the
documentation for Devel::Cover for more information.

The -test option will delete the databases and run your tests to generate
new coverage data before reporting on it.  Devel::Cover knows how to work
with standard Perl Makefiles as well as Module::Build based distributions.
For detailed instructions see the documentation for ExtUtils::MakeMaker at
L<https://metacpan.org/module/ExtUtils::MakeMaker> or for Module::Build at
L<https://metacpan.org/module/Module::Build> both of which come as standard
in recent Perl distributions.

The -gcov option will try to run gcov on any XS code.  This requires that
you are using gcc of course.  If you are using the -test option will be
turned on by default.

=head1 EXIT STATUS

The following exit values are returned:

0   All operations were completed successfully.

>0  An error occurred.

With the -test option the exit status of the underlying test run is retuened.

=head1 SEE ALSO

 L<Devel::Cover>

=head1 BUGS

Did I mention that this is alpha code?

See the BUGS file.

=head1 LICENCE

Copyright 2001-2013, Paul Johnson (paul@pjcj.net)

This software is free.  It is licensed under the same terms as Perl itself.

The latest version of this software should be available from my homepage:
http://www.pjcj.net

=cut
