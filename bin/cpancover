#!/usr/bin/perl

# Copyright 2002-2013, Paul Johnson (paul@pjcj.net)

# This software is free.  It is licensed under the same terms as Perl itself.

# The latest version of this software should be available from my homepage:
# http://www.pjcj.net

require 5.6.1;

use strict;
use warnings;

# VERSION

use lib qw( ./lib );
use Pod::Usage;
use Devel::Cover::DB;
use Devel::Cover::Dumper;
use Devel::Cover::cpancover::Defaults qw(
    %defaults
    get_options
);
use Devel::Cover::cpancover::Template::Provider;
use Devel::Cover::cpancover qw(
    read_results
    write_html
    run_cover
);

use Getopt::Long;
use Template 2.00;


$|++;

my $config = semi_get_options();

my $Template = Template->new( {
    LOAD_TEMPLATES =>
    [
        Devel::Cover::cpancover::Template::Provider->new({}),
    ],
} );

if ($config->{collect}) {
    $config = run_cover($config);
}

write_html($config, $Template);

######### SUBROUTINES ##########

sub semi_get_options {
    my $Options = get_options(\%defaults);

    print "$0 version " . __PACKAGE__->VERSION . "\n" and exit 0
        if $Options->{version};
    pod2usage(-exitval => 0, -verbose => 0)  if $Options->{help};
    pod2usage(-exitval => 0, -verbose => 2)  if $Options->{info};

    # Processing of command-line options has been completed;
    # processing of files listed on command-line remains.

    my $config;
    while (my ($k,$v) = each(%{$Options})) {
        $config->{$k} = $v;
    }
    $config->{outputdir}  ||= $config->{directory};
    $config->{outputfile} ||= "coverage.html";
    push @{$config->{module}}, @ARGV;
    if (!$config->{redo_cpancover_html} && !@{$config->{module}})
    {
        my $d = $config->{directory};
        opendir my $D, $d or die "Can't opendir $d: $!\n";
        @{$config->{module}} = grep !/^\./ && -e "$d/$_/Makefile.PL",
                                     sort readdir $D
            or die "No module directories found\n";
        closedir $D or die "Can't closedir $d: $!\n";
    }
    return $config;
}

__END__

=head1 NAME

cpancover - report coverage statistics on CPAN modules

=head1 SYNOPSIS

 cpancover -help -info -version

=head1 DESCRIPTION


=head1 OPTIONS

The following command line options are supported:

    -h -help                - show help
    -i -info                - show documentation
    -v -version             - show version
    -collect                - run 'cover' on modules; default is 'on'
    -directory              - directory under which to search for modules
                              needing coverage analysis
    -force                  - force re-rebuild and re-test of module even if
                              'cover' has already been run
    -module                 - ???
    -outputdir              - directory into which overall coverage report
                              will be placed; defaults to value of 'directory'
                              option or current directory
    -outputfile             - name of overall summary file; defaults to
                              'coverage.html'
    -redo_cpancover_html    - defaults to 'off'
    -redo_html              - defaults to 'off'
    -report                 - type of output generated; defaults to
                              'html_basic'

=head1 DETAILS


=head1 REQUIREMENTS

The modules L<Template> and L<Parallel::Iterator> are required.

=head1 EXIT STATUS

The following exit values are returned:

0   All operations were completed successfully.

>0  An error occurred.

=head1 SEE ALSO

 L<Devel::Cover>

=head1 BUGS

=head1 LICENCE

Copyright 2002-2013, Paul Johnson (paul@pjcj.net)

This software is free.  It is licensed under the same terms as Perl itself.

The latest version of this software should be available from my homepage:
http://www.pjcj.net

=cut

__END__
# use Carp; $SIG{__DIE__} = \&Carp::confess;
